/*
 * Copyright (c) 2023, Patrick Bene
 * This file is distributed under the Boost Software License, Version 1.0.
 * See LICENSE_1_0.txt or https://www.boost.org/LICENSE_1_0.txt 
 */

mode main()
    // Load the palette:
    palette = example_palette
    ppu_upload_palette()

    // Setup the nametable:
    ppu_reset_addr($2000)
    for U y = 0; y < 30; y += 1
        for U x = 0; x < 32; x += 1
            {PPUDATA}((x & %1111) + ((y & %1111) << 4))
    for U a = 0; a < 64; a += 1
        {PPUDATA}($FF)

    // Setup extended attributes:
    {MMC5_EXRAM_MODE}(%10) // Use an EXRAM mode that allows writing.
    MM ptr = mmc5_exram
    do for U i = 0; i; i += 1
        do for U j = 0; j < 4; j += 1
            ptr[j] = rand() << 6
        ptr += 4
    
    // Set MMC5 registers:
    {MMC5_EXRAM_MODE}(%01) // Use extended attribute mode.
    mmc5_mirror_1()        // Use 1-screen mirroring.

    // Loop forever:
    {PPUCTRL}(PPUCTRL_NMI_ON | PPUCTRL_SPR_PT_1000)
    while true
        nmi
        ppu_reset_scroll(0, 0)
        {PPUMASK}(PPUMASK_BG_ON | PPUMASK_NO_CLIP)

chrrom
    file(fmt, "bg.png")
