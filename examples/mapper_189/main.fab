/*
 * Copyright (c) 2023, Patrick Bene
 * This file is distributed under the Boost Software License, Version 1.0.
 * See LICENSE_1_0.txt or https://www.boost.org/LICENSE_1_0.txt 
 */

// This examples uses scanline IRQs to alter the scroll mid-frame.

fn split()
    {PPUSCROLL}(rand() & %11)
    {PPUSCROLL}(0)
    mmc3_timer(10)

irq main_irq()
    split()

mode main()
: irq main_irq
    palette = example_palette
    ppu_upload_palette()

    // Setup the nametable:
    ppu_reset_addr($2000)
    for U y = 0; y < 30; y += 1
        for U x = 0; x < 32; x += 1
            {PPUDATA}((x & %1111) + ((y & %1111) << 4))
    for U a = 0; a < 64; a += 1
        {PPUDATA}(0)

    // Setup MMC3:
    {MMC3_MIRRORING}(1)
    {MMC3_BANK_SELECT, MMC3_BANK_DATA}(0, %00)
    {MMC3_BANK_SELECT, MMC3_BANK_DATA}(1, %10)
    
    // Enable interrupts:
    {MMC3_IRQ_DISABLE}(0)
    irq true

    {PPUCTRL}(PPUCTRL_NMI_ON | PPUCTRL_SPR_PT_1000)
    while true
        nmi
        ppu_reset_scroll(0, 0)
        split()
        {PPUMASK}(PPUMASK_BG_ON | PPUMASK_NO_CLIP)

chrrom
    file(fmt, "bg.png")
